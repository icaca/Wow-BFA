## Demonology Warlock
## March 14, 2021

## Changes:
## - Merged some precombat and utility actions from SimC APL.
## - Made Tyrant castable when Vilefiend or Felguard is falling off, even without Imps.

## Executed before combat begins. Accepts non-harmful actions only.
actions.precombat=fel_domination,if=time>0&!pet.alive
actions.precombat+=/summon_pet
actions.precombat+=/fleshcraft
actions.precombat+=/power_siphon,if=buff.wild_imps.stack>1&buff.demonic_core.stack<=2
actions.precombat+=/demonbolt,if=(boss|action.demonbolt.cast=0)&soul_shards<4
actions.precombat+=/shadow_bolt,if=soul_shards<5

# Executed every time the actor is available.
actions=spell_lock
actions+=/call_felhunter
actions+=/axe_toss
actions+=/devour_magic
actions+=/call_action_list,name=covenant_ability
actions+=/call_action_list,name=special_trinkets
actions+=/call_action_list,name=tyrant_setup
actions+=/summon_demonic_tyrant,if=buff.wild_imps.stack+imps_spawned_during.summon_demonic_tyrant>3|buff.grimoire_felguard.up&buff.grimoire_felguard.remains>2*haste&buff.grimoire_felguard.remains<4|buff.vilefiend.up&buff.vilefiend.remains>2*haste&buff.vilefiend.remains<4|buff.dreadstalkers.up&buff.dreadstalkers.remains<4
actions+=/call_action_list,name=during_tyrant,if=pet.demonic_tyrant.active
actions+=/demonic_strength,if=!runeforge.wilfreds_sigil_of_superior_summoning&cooldown.summon_demonic_tyrant.remains>9
actions+=/call_dreadstalkers,if=cooldown.summon_demonic_tyrant.remains>20
actions+=/doom,if=refreshable
actions+=/power_siphon,if=buff.wild_imps.stack>1&talent.power_siphon.enabled&buff.demonic_core.stack==0&!pet.dreadstalker.active
actions+=/bilescourge_bombers
actions+=/implosion,if=active_enemies>1&!talent.sacrificed_souls.enabled&buff.wild_imps.stack>=8&buff.tyrant.down&cooldown.summon_demonic_tyrant.remains>5
actions+=/implosion,if=active_enemies>2&buff.wild_imps.stack>=8&buff.tyrant.down&!runeforge.implosive_potential
actions+=/implosion,if=active_enemies>2&buff.wild_imps.stack>=6&buff.implosive_potential.remains<2&runeforge.implosive_potential
actions+=/use_items,if=pet.demonic_tyrant.active
actions+=/call_action_list,name=fillers

actions.tyrant_setup=nether_portal,if=cooldown.summon_demonic_tyrant.remains<20
actions.tyrant_setup+=/call_action_list,name=wilfreds_setup,if=runeforge.wilfreds_sigil_of_superior_summoning
actions.tyrant_setup+=/call_action_list,name=no_wilfreds_setup,if=!runeforge.wilfreds_sigil_of_superior_summoning&!talent.grimoire_felguard.enabled
actions.tyrant_setup+=/call_action_list,name=no_wilfreds_gfg_setup,if=!runeforge.wilfreds_sigil_of_superior_summoning&talent.grimoire_felguard.enabled


## NIGHT FAE
# Base usage: Every Tyrant setup.
actions.covenant_ability=soul_rot,if=soulbind.grove_invigoration&cooldown.summon_demonic_tyrant.remains<20
# With Field of Blossoms, just pop it as soon as Tyrant is up.
actions.covenant_ability+=/soul_rot,if=soulbind.field_of_blossoms&pet.demonic_tyrant.active
# With Grimoire Felguard and no Wilfred's: Hold Tyrant for every GrimFG and one more usage of Soul Rot in-between.
actions.covenant_ability+=/soul_rot,if=!runeforge.wilfreds_sigil_of_superior_summoning&talent.grimoire_felguard.enabled&cooldown.grimoire_felguard.remains>40
# With Korayn: Just use it whenever.
actions.covenant_ability+=/soul_rot,if=soulbind.wild_hunt_tactics

## NECROLORD
## Emeni
## Using Fleshcraft just for the 5% health buff and then canceling is actually a dps loss.
## actions.covenant_ability+=/fleshcraft,if=soulbind.lead_by_example&cooldown.summon_demonic_tyrant.remains<20
## actions.covenant_ability+=/cancel_action,if=action.fleshcraft.channeling
# For 5-13% int right after Tyrant.
actions.covenant_ability+=/decimating_bolt,if=soulbind.lead_by_example&pet.demonic_tyrant.active
# Other Necrolord soulbinds.
actions.covenant_ability+=/decimating_bolt,if=!soulbind.lead_by_example&!pet.demonic_tyrant.active

## KYRIAN
# Pelagos: cast only when Tyrant is active to ensure mastery buff on Tyrant?
actions.covenant_ability+=/scouring_tithe,if=soulbind.combat_meditation&pet.demonic_tyrant.active
# Maintenance dot otherwise.
actions.covenant_ability+=/scouring_tithe,if=!soulbind.combat_meditation

## VENTHYR - worth casting, but no synergy that's worth holding it
actions.covenant_ability+=/impending_catastrophe


# Once per minute: Big Summons > Imps > Tyrant
actions.wilfreds_setup=grimoire_felguard,if=talent.grimoire_felguard.enabled&cooldown.summon_demonic_tyrant.remains<18
actions.wilfreds_setup+=/summon_vilefiend,if=talent.summon_vilefiend.enabled&cooldown.summon_demonic_tyrant.remains<15
actions.wilfreds_setup+=/call_dreadstalkers,if=cooldown.summon_demonic_tyrant.remains<12

# No GrimFG:  Tyrant every 90, VF every 45 sec.
actions.no_wilfreds_setup=summon_vilefiend,if=talent.summon_vilefiend.enabled&(cooldown.summon_demonic_tyrant.remains<13|cooldown.summon_demonic_tyrant.remains>45)
actions.no_wilfreds_setup+=/call_dreadstalkers,if=cooldown.summon_demonic_tyrant.remains<10

# GrimFG: Tyrant every 120, VF in-between with Soul Rot.
actions.no_wilfreds_gfg_setup=grimoire_felguard,if=cooldown.summon_demonic_tyrant.remains<15
actions.no_wilfreds_gfg_setup+=/summon_vilefiend,if=talent.summon_vilefiend.enabled&((cooldown.summon_demonic_tyrant.remains<12&cooldown.grimoire_felguard.remains<10)|cooldown.grimoire_felguard.remains>40)
actions.no_wilfreds_gfg_setup+=/call_dreadstalkers,if=cooldown.summon_demonic_tyrant.remains<10

actions.special_trinkets=call_action_list,name=hp_trinks,if=talent.demonic_consumption&cooldown.summon_demonic_tyrant.remains<20
actions.special_trinkets+=/use_item,name=empyreal_ordnance,if=cooldown.summon_demonic_tyrant.remains<15
actions.special_trinkets+=/use_item,name=soulletting_ruby,if=cooldown.summon_demonic_tyrant.remains<7

actions.hp_trinks+=/use_item,name=sinful_gladiators_emblem
actions.hp_trinks+=/use_item,name=sinful_aspirants_emblem

actions.during_tyrant=berserking
actions.during_tyrant+=/blood_fury
actions.during_tyrant+=/fireblood
actions.during_tyrant+=/potion
actions.during_tyrant+=/demonic_strength
actions.during_tyrant+=/use_items

actions.tyrant_main=summon_demonic_tyrant,if=
(
    (!talent.summon_vilefiend.enabled|pet.vilefiend.active)&
    (!talent.grimoire_felguard.enabled|buff.grimoire_felguard.up|cooldown.grimoire_felguard.remains>30)&
    (pet.dreadstalker.remains<4|(!talent.grimoire_felguard.enabled|buff.grimoire_felguard.remains<4)|buff.vilefiend.remains<4)
)

## Pop big summons when fight is about to end.
actions.fillers=grimoire_felguard,if=boss&fight_remains<32
actions.fillers+=/summon_vilefiend,if=boss&fight_remains<30
actions.fillers+=/summon_demonic_tyrant,if=boss&fight_remains<15

## Actual fillers
actions.fillers+=/hand_of_guldan,if=soul_shard=5
actions.fillers+=/hand_of_guldan,if=soul_shard>=3&(pet.dreadstalker.active|pet.demonic_tyrant.active)
actions.fillers+=/hand_of_guldan,if=soul_shard>=1&buff.nether_portal.up
actions.fillers+=/demonbolt,if=buff.demonic_core.react&soul_shard<4
actions.fillers+=/soul_strike
actions.fillers+=/shadow_bolt
